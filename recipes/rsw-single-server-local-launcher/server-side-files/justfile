set dotenv-load

RSW_LICENSE := env_var("RSW_LICENSE")
RSW_URL := env_var("RSW_URL")
RSW_FILENAME := env_var("RSW_FILENAME")

R_VERSION := env_var_or_default("R_VERSION", "4.1.2")
PYTHON_VERSION := env_var_or_default("PYTHON_VERSION", "3.10.4")

# -----------------------------------------------------------------------------
# Build RSW
# -----------------------------------------------------------------------------

# Install RStudio workbench and all of the dependencies
build-rsw: 
    # Basic setup
    sudo apt-get update
    sudo apt-get update
    sudo apt-get install -y gdebi-core

    # Add some test users
    just add-user sam password
    just add-user jake password
    just add-user olivia password

    # Install RSW and required dependencies
    just install-r 
    just symlink-r
    just install-python
    just install-rsw
    just install-vscode
    sudo cp -r /etc/rstudio /etc/rstudio-original-conf-files

    # Set up SSL
    just ssl-copy-files
    
    # Set up config files
    just copy-config-files

    # Restart
    sudo rstudio-server restart

    # For some reason sudo rstudio-launcher restart is throwing an error.  
    # Use the || operator so that justfile does not think the recipe failed.
    sudo rstudio-launcher restart || echo

# -----------------------------------------------------------------------------
# Install
# -----------------------------------------------------------------------------

install-rsw:
    curl -O {{RSW_URL}}
    sudo gdebi -n {{RSW_FILENAME}}
    sudo rstudio-server license-manager activate $RSW_LICENSE

install-r:
    curl -O https://cdn.rstudio.com/r/ubuntu-2004/pkgs/r-{{R_VERSION}}_1_amd64.deb
    sudo gdebi -n r-{{R_VERSION}}_1_amd64.deb

install-python:
    # https://docs.rstudio.com/resources/install-python/
    # install python
    curl -O https://cdn.rstudio.com/python/ubuntu-2004/pkgs/python-{{PYTHON_VERSION}}_1_amd64.deb
    sudo gdebi -n python-{{PYTHON_VERSION}}_1_amd64.deb
    sudo /opt/python/{{PYTHON_VERSION}}/bin/python3 -m pip install --upgrade pip setuptools wheel
    # create a jupyter kernel
    sudo /opt/python/{{PYTHON_VERSION}}/bin/python3 -m pip install ipykernel
    sudo /opt/python/{{PYTHON_VERSION}}/bin/python3 -m ipykernel install --name py{{PYTHON_VERSION}} --display-name "Python {{PYTHON_VERSION}}"

install-vscode:
    sudo rstudio-server install-vs-code /opt/code-server

# -----------------------------------------------------------------------------
# Config
# -----------------------------------------------------------------------------

copy-config-files:
    sudo cp ~/rserver.conf /etc/rstudio/rserver.conf
    sudo cp ~/launcher.conf /etc/rstudio/launcher.conf
    sudo cp ~/vscode.extensions.conf /etc/rstudio/vscode.extensions.conf

ssl-copy-files:
    sudo cp ~/server.key /etc/ssl/server.key
    sudo chmod 600 /etc/ssl/server.key
    sudo cp ~/server.crt /etc/ssl/server.crt
    sudo chmod 600 /etc/ssl/server.crt

# -----------------------------------------------------------------------------
# Linux mgmt
# -----------------------------------------------------------------------------

add-user name password:
    #!/bin/bash
    sudo useradd --create-home --home-dir /home/{{name}} -s /bin/bash {{name}};
    echo -e '{{password}}\n{{password}}' | sudo passwd {{name}};

symlink-r:
    sudo ln -s /opt/R/{{R_VERSION}}/bin/R /usr/local/bin/R
    sudo ln -s /opt/R/{{R_VERSION}}/bin/Rscript /usr/local/bin/Rscript